apply plugin: 'com.android.application'

// [Gradle 编译配置]相关的提交内容见博客【Android Gradle 常用配置】
android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.csp.sample.customview" // TODO 模板，包名修改
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // java 1.8
    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    // 自动签名
    signingConfigs {
        // 加载配置文件
        File propertiesFile = project.rootProject.file('keystore/keystore.properties')
        Properties properties = new Properties()
        properties.load(propertiesFile.newDataInputStream())

        // 配置自动签名
        release {
            storeFile file(properties['KEY_APP_STORE_FILE'])
            storePassword properties['KEY_STORE_PASSWORD']
            keyAlias properties['KEY_ALIAS']
            keyPassword properties['KEY_PASSWORD']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release // 自动签名
        }

        release {
            signingConfig signingConfigs.release
            debuggable false
            zipAlignEnabled true
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation dep.appcompat

    // 单元测试
    testImplementation dep.junit
    androidTestImplementation dep.junit_ext
    androidTestImplementation dep.espresso_core

    // 其他 Module
    implementation project(':lib-customview')
}
